// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.2.2' apply false
    id 'com.android.library' version '8.2.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.22' apply false
    id 'com.google.dagger.hilt.android' version '2.50' apply false
    id 'com.google.devtools.ksp' version '1.9.22-1.0.17' apply false
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "17"
            freeCompilerArgs += [
                    "-Xjvm-default=all",
                    "-Xcontext-receivers"
            ]
        }
    }

    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    pluginManager.withPlugin('com.android.application') {
        configureAndroidProject(project)
    }
    pluginManager.withPlugin('com.android.library') {
        configureAndroidProject(project)
    }
}

def configureAndroidProject(project) {
    project.android {
        compileSdk 34

        defaultConfig {
            minSdk 21
            targetSdk 34
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
            coreLibraryDesugaringEnabled true
        }

        buildFeatures {
            buildConfig true
        }
    }

    project.dependencies {
        coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
    }
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}